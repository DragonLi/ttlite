-- basic functions over natural numbers
import "core.pi";

-- addition of natural numbers
let plus =
  natElim
    ( \ _ -> Nat -> Nat )              -- motive
    ( \ n -> n )                       -- case for Zero
    ( \ _ rec -> \n -> Succ (rec n));  -- case for Succ

-- alternative (more readable??) variation of plus
let plus1 =
  (\x y ->
    natElim
    ( \ _ -> Nat )                -- motive
    y                             -- case for Zero
    (\ n rec -> Succ rec) x) ::   -- case for Succ
    forall (a :: Nat) (b :: Nat) . Nat;

-- predecessor, mapping 0 to 0
let pred =
  natElim
    ( \ _ -> Nat )
    Zero
    ( \ n1 _ -> n1 );

-- fZSucc0 Z        = a1 :: A
-- fZSucc0 (Succ _) = a2 :: A
-- this function allows to eliminate nats
-- in simple (homo) way
let fZSucc0 =
    (\A a1 a2 -> natElim (\n -> A) a1 (\n pred -> a2))
    :: forall (A :: *) (a1 :: A) (a2 :: A) . Nat -> A;

-- fZSucc0 Z        = a :: A
-- fZSucc0 (Succ _) = b :: b
-- note that the type of result depends on n
let fZSucc =
    (\A B a b -> natElim (\n -> fZSucc0 * A B n) a (\n pred -> b) )
    :: forall (A :: *) (B :: *) (_ :: A) (_ :: B) (n :: Nat) . fZSucc0 * A B n;

-- a simpler elimination scheme for natural numbers
-- type of result doesn't depend on n
let natFold =
  ( \ m mz ms -> natElim
                   (const * Nat m)
                   mz
                   (\ n1 rec -> ms rec ) )
  :: forall (a :: *) . a -> (a -> a) -> Nat -> a;

-- an eliminator for natural numbers that has special
-- cases for 0 and 1
let nat1Elim =
  ( \ m m0 m1 ms -> natElim
                      (m :: Nat -> *)
                      (m0 :: m 0)
                      ( (\ n1 -> ((\ _ -> ((natElim
                                    (\ n -> m (Succ n))
                                    m1
                                    ms :: forall (a :: Nat) . m (Succ a))
                                    n1) ) )) :: forall (nn :: Nat) (_ :: m nn) . m (Succ nn) ))
  :: forall (m :: Nat -> *) . m 0 -> m 1 ->
     (forall n :: Nat . m (Succ n) -> m (Succ (Succ n))) ->
     forall (n :: Nat) . m n;

-- an eliminator for natural numbers that has special
-- cases for 0, 1 and 2
let nat2Elim =
  ( \ m m0 m1 m2 ms -> nat1Elim m m0 m1
                                (\ p rec -> natElim (\ n -> m (Succ (Succ n))) m2 ms p) )
  :: forall (m :: Nat -> *) . m 0 -> m 1 -> m 2 ->
     (forall n :: Nat . m (Succ (Succ n)) -> m (Succ (Succ (Succ n)))) ->
     forall (n :: Nat) . m n;

-- increment by one
let inc = natFold Nat (Succ Zero) Succ;

let nRight = forall n :: Nat . Eq Nat (plus n 0) n;
// the other direction requires induction on N:
let pNPlus0isN =
  natElim ( \ n -> Eq Nat (plus n 0) n )
          ( Refl Nat 0 )
          ( \ n1 rec -> leibniz Nat Nat Succ (plus n1 0) n1 rec )
  :: nRight;
